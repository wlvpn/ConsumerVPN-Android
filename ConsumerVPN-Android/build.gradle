
buildscript {
    ext.kotlin_version = '1.2.30'
    ext.gentleBreezeGradleUtils = '1.2.15719'

    repositories {
        mavenLocal()
        jcenter()
        google()
        maven {
            url "https://packagecloud.io/priv/${packagecloud_vpn_token}/cloak/android-vpn-sdk/maven2"
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.2'
        classpath 'org.ajoberstar:gradle-git:1.7.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.gentlebreeze.gradle:ci-utils:$gentleBreezeGradleUtils"
    }
}

allprojects {
    version = property("android.versionName")
    group = property('pom.groupId')

    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://maven.google.com' }
        maven {
            url "https://packagecloud.io/priv/${packagecloud_vpn_token}/cloak/android-vpn-sdk/maven2"
        }
    }
}

ext {
    sourceCompatibilityVersion = JavaVersion.VERSION_1_8
    targetCompatibilityVersion = JavaVersion.VERSION_1_8
}

ext.utils = [
        /**
         * Helper to use java util map
         */
        javaMapType     : "java.util.Map<Integer, Boolean>",

        /**
         * Helper to set Connecting options for different build types
         * Check uses build.gradle on WLVPN Module
         *
         * @param showSecure To show secure tab
         * @param showFast To show fast tab
         * @param showFastest To show fastest tab
         */
        getTabsMapValues: { boolean showSecure, boolean showFast, boolean showFastest ->
            def javaNewMapType = "new java.util.HashMap<Integer, Boolean>() {{[#values#]}}"

            def result = javaNewMapType.replace("[#values#]",
                    "put(0, ${showSecure});" +
                            "put(1,  ${showFast});" +
                            "put(2,  ${showFastest});"
            )

            return result
        }
]